; bootsect.asm
; Created by Matheus Leme Da Silva
BITS 16
ORG 0x7C00

%DEFINE ADDRESS 0x8000
%DEFINE START_SECTOR 1
%DEFINE SECTOR_COUNT 62

%DEFINE LSEGMENT (ADDRESS >> 4)
%DEFINE LOFFSET (ADDRESS & 0xF)

_START:
	CLI
	XOR AX, AX
	MOV DS, AX
	MOV ES, AX
	MOV SS, AX
	MOV SP, 0x7C00
	STI

	MOV [BOOT_DRIVE], DL

	MOV SI, LOADING_MSG
	CALL PRINT_STRING

	MOV EDI, ADDRESS
	MOV ECX, START_SECTOR

LOAD_LOOP:
	CMP ECX, SECTOR_COUNT
	JG JUMP

	CALL DISK_READ_SECTOR

	MOV AH, 0xE
	MOV AL, '.'
	INT 0x10

	INC ECX
	ADD EDI, 512
	JMP LOAD_LOOP

JUMP:
	MOV DL, [BOOT_DRIVE]
	JMP (ADDRESS>>4):(ADDRESS&0xF)
	JMP HANG

; Handles error and halts the system
ERROR:
	MOV SI, ERROR_MSG
	CALL PRINT_STRING
HANG:
	JMP HANG

; Read sector in ECX at EDI address
; BIOS returns AH != 0 if have error
DISK_READ_SECTOR:
	PUSH EDI
	AND EDI, 0xF
	MOV [DISK_PACKET.OFFSET], DI
	POP EDI

	PUSH EDI
	SHR EDI, 4
	MOV [DISK_PACKET.SEGMENT], DI
	POP EDI

	MOV [DISK_PACKET.LBA_LOW], ECX

	MOV AH, 0x42
	MOV DL, [BOOT_DRIVE]
	MOV SI, DISK_PACKET
	INT 0x13
	CMP AH, 0
	JNE ERROR
	RET

; Prints a string in DS:SI while no \0
PRINT_STRING:
	PUSH AX
	PUSH SI
	.LOOP:
		LODSB
		CMP AL, 0
		JE .EXIT
		MOV AH, 0xE
		INT 0x10
		JMP .LOOP
	.EXIT:
		POP AX
		POP SI
		RET

LOADING_MSG: DB "Loading...", 0
ERROR_MSG: DB "ERROR!",0
BOOT_DRIVE: DB 0

DISK_PACKET:
	.SIZE: DB 0x10
	.ZERO: DB 0
	.SECTORS: DW 1
	.OFFSET: DW 0
	.SEGMENT: DW 0
	.LBA_LOW: DD 0
	.LBA_HIGH: DD 0

TIMES 446 - ($ - $$) DB 0
TIMES 510 - ($ - $$) DB 0
DW 0xAA55

